plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.2'
group = 'bl4ckscor3.mod.nolancheating'

base {
	archivesName = 'nolancheating-forge-1.20.1'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
	mappings channel: 'parchment', version: '2023.09.03-1.20.1'
	copyIdeResources = true
	
    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'
            args '-mixin.config=nolancheating.mixins.json'

            mods {
                nolancheating {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.20.1-47.1.3'
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation fg.deobf("curse.maven:mcwifipnp-450250:5056240")
}

mixin {
    add sourceSets.main, "nolancheating.refmap.json"
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title": "No LAN Cheating",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "No LAN Cheating",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3",
                    "MixinConfigs": "nolancheating.mixins.json"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}